       IDENTIFICATION                              DIVISION. 
      ******************************************************************
       PROGRAM-ID. CRIAR-CHAMADA.
      ******************************************************************
       ENVIRONMENT                                 DIVISION. 
      ******************************************************************
       INPUT-OUTPUT                      SECTION. 
      *-----------------------------------------------------------------
       FILE-CONTROL.

           SELECT OPTIONAL REG-COURS ASSIGN TO "enreg-cours.dat"
           ORGANIZATION IS INDEXED
           ACCESS MODE IS DYNAMIC
           RECORD KEY IS REG-ID-COURS
           FILE STATUS IS FS-REG-COURS.

           SELECT OPTIONAL LISTE-APPEL ASSIGN TO "liste-appel.dat"
           ORGANIZATION IS INDEXED
           ACCESS MODE IS DYNAMIC
           RECORD KEY IS LISTE-ID-COURS
           FILE STATUS IS FS-LISTE-APPEL.

           SELECT PRINT-APPEL ASSIGN TO "print-file2.txt"
           ORGANIZATION IS LINE SEQUENTIAL
           FILE STATUS IS FS-PRINT-APPEL.
      
      ******************************************************************
       DATA                                        DIVISION.
      ******************************************************************
       FILE                              SECTION.
      *-----------------------------------------------------------------
      
       FD  REG-COURS.
       01  CAD.
           02 REG-EOF-LIGNE PIC X.
           02 REG-ID-COURS PIC 9(5).
           02 REG-NOM-COURS PIC X(30).
           02 REG-ELEVE OCCURS 20 INDEXED BY FD-REG.
              03 REG-NUMERO-ELEVE PIC 99.
              03 REG-NOM-COMPLET PIC X(40).
              03 REG-INSCRIP PIC 9(5).
       
       FD  LISTE-APPEL.
       01  LISTE.
           88 LISTE-EOF VALUE HIGH-VALUES.
           02 LISTE-ID-COURS PIC 9(5).
           02 LISTE-NOM-COURS PIC X(30).
           02 LISTE-ELEVE OCCURS 20 INDEXED BY FD-LISTE.
              03 LISTE-NUMERO-ELEVE PIC 99.
              03 LISTE-NOM-COMPLET PIC X(40).
              03 LISTE-INSCRIP PIC 9(5).

       FD  PRINT-APPEL.
       01  LINHA-GRAVACAO PIC X(90).
      *-----------------------------------------------------------------
       WORKING-STORAGE                   SECTION.
      *-----------------------------------------------------------------

      *    VARIABLES DE CONTRÔLE D'ERREURS
       
       01  FS-REG-COURS PIC 99.
           88  FS-REG-COURS-OK VALUE '00'.
       
       01  FS-LISTE-APPEL PIC 99.
           88 FS-LISTE-APPEL-OK VALUE '00'.
           88 FS-LISTE-APPEL-CRIAR VALUE '05'.

       01  FS-PRINT-APPEL PIC 99.
           88 FS-PRINT-APPEL-OK VALUE '00'.

      *    VARIABLES DE RANGEMENT DE LA LISTE D'APPEL

       01  WS-LISTE.
           02 WS-EOF-LIGNE PIC X.
           02 WS-ID-COURS PIC 9(5).
           02 WS-NOM-COURS PIC X(30) VALUE SPACES.
           02 WS-ELEVE OCCURS 20 INDEXED BY I-ELEVE-WS.
              03 WS-NUMERO-ELEVE PIC 99.
              03 WS-NOM-COMPLET PIC X(40) VALUE SPACES.
              03 WS-INSCRIPT PIC 9(5).

       01  VECTEUR-RECORD OCCURS 21 INDEXED BY IDX-VET.
           02 VECTEUR-NUMERO-ELEVE PIC 99.
           02 VECTEUR-NOM-COMPLET PIC X(40).
           02 VECTEUR-INSCRIPT PIC 9(5).

       77  WS-EOF PIC X VALUE 'N'.
       77  WS-CONTROL-COURS PIC 9(5).
       77  WS-EQUILIBRAGE PIC 9(4).

       77  WS-COMPT1 PIC 99.
       77  WS-COMPT2 PIC 99.
       77  WS-COMPT3 PIC 99.
       77  WS-COMPT4 PIC 99.
       77  WS-COMPT5 PIC 99.
       77  WS-COMPT6 PIC 99.
       77  WS-COMPT7 PIC 99.


      *    VARIABLE DU FICHIER PRINT

       01  PRINT-CONTROL.
           02 WS-PRINT-PAGES.
              03 FILLER PIC X(35) VALUE SPACES.
              03 WS-COMPT-PAGE PIC 9(4) VALUE 1.
       
       01  PRINT-TETE-1.
           02 FILLER PIC X(28) VALUE SPACES.
           02 FILLER PIC X(20) VALUE 'SORBONNE UNIVERSITE'.

       01  PRINT-TETE-2.
           02 FILLER PIC X(16) VALUE SPACES.
           02 WS-TETE-ID-COURS PIC X(5).
           02 FILLER PIC X(30) VALUE SPACES.
           02 WS-TETE-COURS PIC X(20).

       01  PRINT-TETE-3.
           02 FILLER PIC X(5) VALUE SPACES.
           02 FILLER PIC X(10) VALUE 'NUMERO'.
           02 FILLER PIC X(10) VALUE SPACES.
           02 FILLER PIC X(40) VALUE 'NOM COMPLET'.
           02 FILLER PIC x(11) VALUE 'INSCRIPTION'.

       01  PRINT-TETE-4.
           02 FILLER PIC X(5) VALUE SPACES.
           02 FILLER PIC X(10) VALUE '----------'.
           02 FILLER PIC X(10) VALUE SPACES.
           02 FILLER PIC X(40) VALUE '---------------------'.
           02 FILLER PIC x(10) VALUE '----------'.

       01  PRINT-INFO-ELEVE.
           02 FILLER PIC X(5) VALUE SPACES.
           02 PRINT-NUMERO PIC 9(5).
           02 FILLER PIC X(15) VALUE SPACES.
           02 PRINT-NOM-COMP PIC X(40).
           02 PRINT-INSCRIPTION PIC x(10).
      ******************************************************************
       PROCEDURE                                   DIVISION.
      ******************************************************************
           
      *    CE PROGRAMME CRÉE UNE LISTE D'APPEL A PARTIR DU FICHIER CRÉE 
      *    PAR LE PROGRAMME ANTERIEUR. EN SUPPOSANT UN COURS AVEC 61 
      *    ÉLÈVES, LE PROGRAMME ANTERIEUR A FAIT 4 CLASSE. LES TROIS 
      *    PREMIÈRE AVEC 20 ÉLÈVES ET LA DERNIÈRE AVEC 1. CE PROGRAMME
      *  FAIT DU ÉQUILIBRAGE DE CLASSES ET LE TRI PAR ORDRE ALPHABETIQUE 

           PERFORM 01000-INICIAR-ARQUIVOS1
           PERFORM 01010-INICIAR-ARQUIVOS2

           PERFORM 02000-GRAVACAO-SAIDA1

           CLOSE REG-COURS

           PERFORM 01020-INICIAR-ARQUIVOS3

           PERFORM 03000-REALTORIO-IMPRESSAO

           CLOSE LISTE-APPEL
           CLOSE PRINT-APPEL

           GOBACK.

      ******************************************************************
       01000-INICIAR-ARQUIVOS1            SECTION.

           OPEN INPUT REG-COURS

           IF NOT FS-REG-COURS-OK 
              DISPLAY "ERREUR D'OUVERTURE DU FICHIER PRIMAIRE"
              GOBACK
           END-IF.

       01000-INICIAR-ARQUIVOS1-FIM.
           EXIT.
      ******************************************************************

      ******************************************************************
       01010-INICIAR-ARQUIVOS2            SECTION.

           OPEN I-O LISTE-APPEL.

           IF NOT FS-LISTE-APPEL-OK 
              IF FS-LISTE-APPEL-CRIAR 
                 DISPLAY 'FICHIER BIEN CRÉÉ'
              ELSE
                 DISPLAY "ERREUR D'OUVERTURE DU FICHIER LISTE D'APPEL"
                 DISPLAY "LA LISTE D'APPEL NE SERA PAS TRAITÉE"
                 GOBACK
              END-IF
           END-IF.

       01010-INICIAR-ARQUIVOS1-FIM.
           EXIT.
      ******************************************************************

      ******************************************************************
       01020-INICIAR-ARQUIVOS3            SECTION.

           OPEN OUTPUT PRINT-APPEL.

           IF NOT FS-PRINT-APPEL-OK 
              DISPLAY "ERREUR D'OUVERTURE DU FICHIER PRINT"
              GOBACK
           END-IF.

       01020-INICIAR-ARQUIVOS1-FIM.
           EXIT.
      ******************************************************************

      ******************************************************************
       02000-GRAVACAO-SAIDA1              SECTION.
           
           MOVE 10100 TO REG-ID-COURS 
           READ REG-COURS 
              KEY IS REG-ID-COURS 
              INVALID KEY DISPLAY 'PROBLÈME DANS LA PREMIÈRE LECTURE'
           END-READ
              
           PERFORM UNTIL LISTE-EOF 

      *    POUR FAIRE DE L'ÉQUILIBRAGE IL FAUT SAVOIR COMBIEN DE CLASSE
      *   ET COMBIEN D'ÉLÈVE DANS LA DERNIER. LA VARIABLE WS-CONTROL
      *   GARDE LA PREMIÈRE CLASSE D'UN COURS E LAISSE LA VARIABLE 
      *   REG-ID PARCOURRIR TOUTES LES CLASSES JUSQU'À LA DERNIÈRE

              MOVE REG-ID-COURS TO WS-CONTROL-COURS

              PERFORM WITH TEST AFTER  VARYING REG-ID-COURS  
                       FROM REG-ID-COURS  BY 1
                       UNTIL WS-EOF = 'I'
              
                 READ REG-COURS 
                    KEY IS REG-ID-COURS 
                    INVALID KEY MOVE 'I' TO WS-EOF 
                 END-READ

              END-PERFORM 
              MOVE 'N' TO WS-EOF
              SUBTRACT 1 FROM REG-ID-COURS    

              READ REG-COURS 
                 KEY IS REG-ID-COURS 
                 INVALID KEY DISPLAY 
                              'ERREUR DE LECTURE DU FICHIER ENREG-COURS'
              END-READ

      *    AVEC LA VARIABLE DE LA PREMIÈRE CLASSE GARDÉE À WS-CONTROL
      *    ET LA DERNIÈRE CLASSE AU BUFFER L'ÉQUILIBRAGE PEUT COMMENCER

              IF REG-ID-COURS (4:2) > ZEROS
                 PERFORM 02010-BALANCEAMENTO
              ELSE 
                 WRITE LISTE FROM CAD
                 MOVE SPACE TO LISTE 
              END-IF

      *    SELON LE SYSTÈME D'ENREGISTREMENT LES COURS SONT RANGÉS PAR 
      *    CENTAINES ET LES CLASSES PAR DIZAINES. DONC ADD 100 FAIT LE
      *    PROGRAMME PASSER AU PROCHAIN COURS

              ADD 100 TO WS-CONTROL-COURS 
              MOVE WS-CONTROL-COURS TO REG-ID-COURS 
              
                 READ REG-COURS 
                    KEY IS REG-ID-COURS
                    INVALID KEY SET LISTE-EOF TO TRUE
                 END-READ
              

           END-PERFORM.   

       02000-GRAVACAO-SAIDA1-FIM.
           EXIT.
      ******************************************************************

      ******************************************************************
       02010-BALANCEAMENTO                SECTION.
      
           PERFORM VARYING FD-REG FROM 1 BY 1 UNTIL 
                    REG-NOM-COMPLET (FD-REG) = SPACE
           END-PERFORM
          
      *    L'INDEX FD-REG GARDE LE PREMIER ESPACE VIDE DE LA CLASSE.
      *    DANS LE CAS DE 61 ÉLÈVE, LE PREMIER SPACE VIDE DE LA DERNIÈRE
      *    CLASSE EST 2. (CHAQUE CLASSE COMPLÈTE A 20 ÉLÈVE)

           MOVE REG-ID-COURS TO WS-COMPT1 

           IF FD-REG < 16
              COMPUTE WS-EQUILIBRAGE = 
                 ((WS-COMPT1 * 20) + (FD-REG - 1))/
                                   (WS-COMPT1 + 1)

      *    CETTE FONCTION DÉTERMINE COMBIEN D'ÉLÈVE PAR CLASSE. DANS LE
      *   CAS DE 61 ELLE FAIT LA DIVISION DE 61 PAR 4 (NOMBRE DE CLASSE)
      *   ET QUI RENVOIE 15. IL NE FAUT PAS QUE L'EQUILIBRAGE SOIT 
      *   PARFAIT. POUR CETTE RAISON ON AJOUTE 1 À LA VARIABLE ET ON 
      *   LA LIMITE LA CRÉATION (WS-COMPT6=18)

              ADD 1 TO WS-EQUILIBRAGE
              MOVE FD-REG TO WS-COMPT6
              SET IDX-VET TO 1

              PERFORM VARYING REG-ID-COURS FROM WS-CONTROL-COURS BY 1 
                       UNTIL REG-ID-COURS (4:2) = (WS-COMPT1) OR 
                       WS-COMPT6 = 18
      
      *    CETTE FONCTION PREND LES DERNIERS ÉLÈVE DE LA CLASSE AVANT 
      *    LE LIMITE DONNÉ PAR WS-EQUILIBRAGE ET PLACE SUR LE VECTEUR. 
      *    DANS LE CAS DE 61 ÉLÈVES LE LIMITE EST 16 ET TOUS LES ÉLÈVES
      *    PLACÉS AU DESSOUS DE 16 SONT DÉPLACÉ VER LE VECTEUR.
      
      *    APRE IL ORDONNE ALPHABÉTIQUEMENT LES ÉLÈVES DE LA CLASSE.
      *    ET ENREGISTRE LE FICHIER LISTE-D'APPEL.TXT. IL RÉPÈTE CETTE 
      *    PROCÉDURE JUSQU'À L'AVANT DERNIÈRE CLASSE.

                 READ REG-COURS
                    KEY IS REG-ID-COURS 
                    INVALID KEY DISPLAY 
                          'ERREUR 2.1 DE LECTURE DU FICHIER ENREG-COURS'
                 END-READ
                 
                 PERFORM VARYING WS-COMPT3 FROM 20 BY -1 UNTIL 
                          WS-COMPT3 = WS-EQUILIBRAGE OR 
                          WS-COMPT6 = 20

                    MOVE REG-ELEVE (WS-COMPT3) TO 
                                       VECTEUR-RECORD (IDX-VET)
                    MOVE SPACE TO REG-ELEVE (WS-COMPT3)
                    SET IDX-VET UP BY 1
                    ADD 1 TO WS-COMPT6

                 END-PERFORM
                
                 PERFORM 02020-ORDEM-ALFABETICA

                 MOVE SPACE TO LISTE
                 MOVE WS-LISTE (2:975) TO LISTE
                 WRITE LISTE 
                    INVALID KEY DISPLAY 'CLÉ DÉJÀ ENREGISTRÉE (1)'
                 END-WRITE
                 MOVE SPACES TO WS-LISTE
                 MOVE SPACE TO CAD (7:970)
                 
              END-PERFORM
      
      *    POUR RÉALISER LE TRAITEMENT DE LA DERNIÈRE CLASSE IL AJOUTE
      *    LE VECTEUR O BUFFER E APRÈS SUIVRE LA MÊME PROCEDURE D'AVANT.

              READ REG-COURS 
                 KEY IS REG-ID-COURS 
                 INVALID KEY DISPLAY 
                          'ERREUR 2.2 DE LECTURE DU FICHIER ENREG-COURS'
              END-READ

              IF REG-NOM-COMPLET (FD-REG) NOT = SPACES 
                 DISPLAY "ERREUR D'ORDRE"
                 GOBACK
              END-IF
              
              MOVE 1 TO WS-COMPT2

              PERFORM VARYING FD-REG FROM FD-REG BY 1
                       UNTIL FD-REG > 20 OR 
                       VECTEUR-NOM-COMPLET (WS-COMPT2) = SPACES
              
                 MOVE VECTEUR-RECORD (WS-COMPT2) TO 
                                      REG-ELEVE (FD-REG) 
                 MOVE SPACE TO VECTEUR-RECORD (WS-COMPT2)
                 
                 ADD 1 TO WS-COMPT2
                 
              END-PERFORM

              PERFORM 02020-ORDEM-ALFABETICA

              MOVE SPACE TO LISTE
              MOVE WS-LISTE (2:975) TO LISTE
              WRITE LISTE
                  INVALID KEY DISPLAY 'CLÉ DÉJÀ ENREGISTRÉE (2)'
              END-WRITE
              MOVE SPACES TO WS-LISTE

           ELSE

      *    L'ELSE EXISTE DANS LE CAS D'UNE CLASSE QUI PORTE PLUS DE 16 
      *    ÉLÈVES DANS SA DERNIÈRE CLASSE. LÀ L'ÉQUILIBRAGE NE S'AVÈRE 
      *    PAS NÉCESSAIRE.

              PERFORM WITH TEST  AFTER VARYING REG-ID-COURS 
                        FROM WS-CONTROL-COURS BY 1 
                        UNTIL REG-ID-COURS (4:2) = WS-COMPT1 
                READ REG-COURS 
                    KEY IS REG-ID-COURS 
                    INVALID KEY DISPLAY 
                          'ERREUR 2.3 DE LECTURE DU FICHIER ENREG-COURS'
                END-READ

                PERFORM 02020-ORDEM-ALFABETICA

                MOVE SPACE TO LISTE
                MOVE WS-LISTE (2:975) TO LISTE
                WRITE LISTE
                    INVALID KEY DISPLAY 'CLÉ DÉJÀ ENREGISTRÉE (3)'
                END-WRITE
                MOVE SPACES TO WS-LISTE

              END-PERFORM

           END-IF.
       02010-BALANCEAMENTO-FIM.
           EXIT.
      ******************************************************************

      ******************************************************************
       02020-ORDEM-ALFABETICA             SECTION.

      *    CETTE SECTION FAIT LE TRANSFÈRE DES DONNÉES DU BUFFER AUX
      *    VARIABLES WS DE FAÇON ALPHABÉTIQUE.

           MOVE SPACES TO WS-LISTE
           MOVE REG-ID-COURS  TO WS-ID-COURS
           MOVE REG-NOM-COURS TO WS-NOM-COURS 

           PERFORM VARYING WS-COMPT2 FROM 1 BY 1 
                    UNTIL REG-NOM-COMPLET (WS-COMPT2) = SPACES 
                    OR WS-COMPT2 > 20
           
              IF WS-COMPT2 = 1
                 MOVE REG-ELEVE (1) TO WS-ELEVE (1)
              ELSE
                 PERFORM VARYING I-ELEVE-WS FROM 1 BY 1 UNTIL 
                          REG-NOM-COMPLET (WS-COMPT2) <= 
                          WS-NOM-COMPLET (I-ELEVE-WS) OR 
                          WS-NOM-COMPLET (I-ELEVE-WS) = SPACE  
                 END-PERFORM
                 
                 IF WS-NOM-COMPLET (I-ELEVE-WS) = SPACES
                    MOVE REG-ELEVE (WS-COMPT2) TO 
                                         WS-ELEVE (I-ELEVE-WS)
                 ELSE 
                    
                    PERFORM VARYING WS-COMPT4 FROM 20 BY -1
                             UNTIL WS-COMPT4 = I-ELEVE-WS

                       COMPUTE WS-COMPT5 = WS-COMPT4 - 1                  
                       MOVE WS-ELEVE (WS-COMPT5) TO 
                                           WS-ELEVE (WS-COMPT4)

                    END-PERFORM

                    MOVE REG-ELEVE (WS-COMPT2) 
                                               TO WS-ELEVE (I-ELEVE-WS)

                 END-IF    
              END-IF      
           END-PERFORM

      *    LES NUMÉROS D'APPEL ATRIBUÉS DANS LE PROGRAMME ANTERIEUR N'ONT
      *   PLUS D'ORDRE. LÀ ON LES REORDONNE. 

           PERFORM VARYING I-ELEVE-WS FROM 1 BY 1 UNTIL 
                    WS-NOM-COMPLET (I-ELEVE-WS) = SPACE OR
                       I-ELEVE-WS > 20
              MOVE I-ELEVE-WS TO WS-NUMERO-ELEVE (I-ELEVE-WS)
           END-PERFORM.
           
       02020-ORDEM-ALFABETICA-FIM.
           EXIT.
      ******************************************************************

      ******************************************************************
       03000-REALTORIO-IMPRESSAO SECTION.
      
      *    LE RAPPORT DES LISTES D'APPEL ET FAIT EN TXT ET D'ACCORD
      *    AVEC LE LAYOUT DE MON LECTEUR PDF. JE METS UNE COPIE DU
      *  FICHIER PDF GÉNÉRÉ PAR LE FICHIER TXT DE SORTIE DE CE PROGRAMME

           MOVE 'N' TO WS-EOF
           MOVE SPACES TO LISTE
           MOVE 10100 TO LISTE-ID-COURS    
           
           READ LISTE-APPEL 
              KEY IS LISTE-ID-COURS 
              INVALID KEY DISPLAY 'ÉCHEC DANS LA LECTURE SEQUENTIAL'
           END-READ

           PERFORM UNTIL WS-EOF = 'Y'
              
              MOVE LISTE-ID-COURS TO WS-TETE-ID-COURS 
              MOVE LISTE-NOM-COURS TO WS-TETE-COURS

              WRITE LINHA-GRAVACAO FROM PRINT-TETE-1 BEFORE 
              ADVANCING 2 LINES 

              WRITE LINHA-GRAVACAO FROM PRINT-TETE-2 BEFORE 
              ADVANCING 2 LINES 

              WRITE LINHA-GRAVACAO FROM PRINT-TETE-3 BEFORE 
              ADVANCING 1 LINES 

              WRITE LINHA-GRAVACAO FROM PRINT-TETE-4 BEFORE 
              ADVANCING 3 LINES 

              PERFORM VARYING WS-COMPT1 FROM 1 BY 1 UNTIL 
                       WS-COMPT1 > 20
                 
                 MOVE LISTE-NUMERO-ELEVE (WS-COMPT1) TO PRINT-NUMERO
                 MOVE LISTE-NOM-COMPLET (WS-COMPT1) TO 
                                                        PRINT-NOM-COMP
                 MOVE LISTE-INSCRIP (WS-COMPT1) TO PRINT-INSCRIPTION
                 
                 WRITE LINHA-GRAVACAO FROM PRINT-INFO-ELEVE BEFORE 
                             ADVANCING 2 LINES 

                 IF WS-COMPT1 = 20
                    WRITE LINHA-GRAVACAO FROM PRINT-INFO-ELEVE BEFORE 
                    ADVANCING 10 LINES 
                 END-IF 

              END-PERFORM   

              WRITE LINHA-GRAVACAO FROM WS-PRINT-PAGES 
                          AFTER ADVANCING 7 LINES
              Move SPACE TO LINHA-GRAVACAO
              WRITE LINHA-GRAVACAO 
              ADD 1 TO WS-COMPT-PAGE  
                           
              READ LISTE-APPEL NEXT RECORD
                 AT END MOVE 'Y' TO WS-EOF 
              END-READ

           END-PERFORM.

       03000-REALTORIO-IMPRESSAO-FIN.
           EXIT.
      ******************************************************************
